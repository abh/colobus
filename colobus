#!/usr/bin/perl -w
# Copyright (c) 2001,2003,2004 Jim Winstead Jr.
# Copyright (c) 2001,2003,2004,2005,2006,2009 Ask BjÃ¸rn Hansen
# See the LICENSE file for details.
use strict;

$| = 1;

# this is designed to be run under tcpserver
# (http://cr.yp.to/ucspi-tcp.html)
# or inetd if you're into that sort of thing

package Colobus;
$Colobus::VERSION = "3.0";

my $TRACE = $ENV{TRACE};
$TRACE++ if -t STDIN and not defined $TRACE;

# spool_feeds will set this to 0 so it can send commands manually
my $RESPOND = 1;

my $REMOTE = $ENV{TCPREMOTEIP} || 'console';

$0 = "colobus: $REMOTE";

use POSIX ();
use DBI ();
use Digest::MD5 qw(md5_hex);
use Mail::Address ();
use Time::Local ();
use Symbol ();
use File::Path qw(mkpath);
use Data::Dumper ();

use Colobus qw(%config %feeds %groups get_group_max open_article);

# setting this to qw(/usr/sbin/sendmail) should work, but DO NOT ADD -t!
my (@mailinject) = qw(/var/qmail/bin/qmail-inject -a);

my ($group,$current); # the current group and article number (state!)

my $config = shift @ARGV || "config";

read_config($config);

# these are the commands we understand, so far
my (@commands) = qw(article body date group head help ihave last list
                    listgroup mode next newgroups newnews post quit slave
                    stat xgtitle xhdr xover xpat xpath xrover xversion);
my (%commands); @commands{@commands} = ('') x @commands;
if ($config{disallow}) {
  foreach (map { s/^\s*(.+?)\s*$/$1/; $_; } split /,/, $config{disallow}) {
    delete $commands{$_};
  }
}

# these are the headers we track for the overview database
my (@overview) = qw(Subject From Date Message-ID References Lines);
my (%overview); @overview{@overview} = ('') x @overview;

my $dsn = $config{'dsn'} || "DBI:mysql:database=colobus;host=localhost";
my $dbh = DBI->connect($dsn, $config{'dbuser'}, $config{'dbpass'}, { PrintError => 1 })
  or die($DBI::errstr);


if (grep { !$groups{$_}->{num} } keys %groups) {
  my $db_groups = $dbh->selectall_hashref(q[select name,id from groups], 'name' );
  my $maxid = 0;
  while (my ($name, $data) = each %$db_groups) {
    my $id = $data->{id};
    $groups{$name}->{num} = $id unless $groups{$name}->{num};
    $maxid = $id if $id > $maxid;
    warn "group num in groups table and config file differs for $name\n"
      unless $groups{$name}->{num} == $id;
  }

  $_ > $maxid and $maxid = $_ for map { $_->{num} } values %groups;

  while (my ($name, $id) = each %groups) {
      next if ($db_groups->{$name});
      my $id = ($groups{$name}->{num}) || ++$maxid;
      $dbh->do(q[insert into groups (name, id) values (?,?)], undef, $name, $id);
  }
}

if (@ARGV && $ARGV[0] eq '-s') {
  spool_feed($ARGV[1]); exit;
}

if (@ARGV && $ARGV[0] eq '-u') {
  update_databases(); exit;
}

my %groupsbyid;
foreach (keys %groups) {
  next unless $groups{$_}->{num};
  $groupsbyid{$groups{$_}->{num}} = $_;
}

my $query = <<END;
SELECT h_subject AS `Subject`, h_from AS `From`, h_date AS `Date`,
       h_messageid AS `Message-ID`,
       h_references AS `References`,
       h_lines AS `Lines`,
       msgid,
       id
 FROM articles
 WHERE group_id = ? AND id >= ? AND id <= ?
END

my $xover_sth = $dbh->prepare($query)
  or die($DBI::errstr);

$SIG{ALRM} = sub { respond(500, "Timeout!"),exit; };

# output the greeting
# XXX provide an option so one could have tcpserver send the banner
respond(200, "$config{servername} - colobus $Colobus::VERSION ready - (posting ok).");

alarm $config{timeout};
while (<STDIN>) {
  $_ =~ s/\r?\n$//s; # advanced chomp
  warn "dispatching $_\n" if $TRACE;
  defined dispatch(split / +/, $_)
    or respond(500, "command '$_' not recognized");
  alarm $config{timeout};
}

sub dispatch {
  my ($cmd) = lc shift;

  if (exists $commands{$cmd}) {
    my ($result) = eval "&$cmd";
    warn $@ if $@;
    return $result if defined $result;
    return fault("command '$cmd' failed unexpectedly");
  }

  return;
}

my $curpos = 0;

sub respond {
  my ($code, $message) = @_;
  return "$code $message" unless $RESPOND;
  warn "$code $message\n" if $TRACE and !-t STDIN;
  my $s = print "$code $message\r\n";
  $curpos = tell STDOUT;
  return $s;
}

sub dot () {
  warn ".\n" if $TRACE and !-t STDIN;
  print ".\r\n";
}

sub fault {
  my ($msg) = shift || "program fault - command not performed";
  return respond(503, $msg);
}

sub note {
  my ($path,$method,$code,$bytes) = @_;
  return unless $path;
  $method ||= 'GET';
  $code ||= 200;
  $bytes ||= 0;
  if ((my $pos = tell STDOUT) > 0) {
    $bytes ||= $pos - $curpos;
  }
  print STDERR "$REMOTE - - ",
	       POSIX::strftime("[%d/%b/%Y:%H:%M:%S %z]", localtime),
	       " \"$method $path HTTP/1.0\" $code $bytes\n";
}

sub get_group_active {
  my ($group) = lc shift
    or return;
  return 'n' unless exists $groups{$group} && $groups{$group}->{mail};
  return 'm' if exists $groups{$group}->{moderated};
  return 'y';
}

sub get_group_description {
  my ($group) = lc shift
    or return;
  return unless exists $groups{$group} && $groups{$group}->{desc};
  return $groups{$group}->{desc};
}

sub get_group_minmax {
  my ($group) = lc shift;
  my $grp = $groups{$group}->{'num'}
    or return;
  return $dbh->selectrow_array("SELECT MIN(id),MAX(id) FROM articles WHERE group_id = ?", undef, $grp);
}

# we guess the group creation time from the modified time of the
# first message in the archive. should be close enough.
sub get_group_creation {
  my ($group) = lc shift
    or return;
  return unless exists $groups{$group};
  my ($artno) = get_group_minmax($group);
  my $file = sprintf("%s/archive/%d/%02d", $groups{$group}->{path},
                     int $artno / 100, $artno % 100);
  return (stat($file))[9];
}

sub get_group_recommend {
  my ($group) = lc shift
    or return;
  return exists $groups{$group} && exists $groups{$group}->{recommend};
}

sub get_article_xover {
  my ($group,$begin,$end) = @_;
  my $grp = $groups{$group}->{'num'}
    or return;

  $xover_sth->execute($grp, $begin, $end || $begin)
    or return;

  my @xover;
  while (my $xover = $xover_sth->fetchrow_hashref) {
    my $msgid = get_article_message_id_from_xover($group,$xover);
    # generate xref and newsgroups info
    my $query = qq{SELECT group_id,id FROM articles WHERE msgid = ?};
    my $res = $dbh->selectall_arrayref($query, undef, md5_hex($msgid));

    $xover->{'xref'}
      = join ' ', map { $groupsbyid{$_->[0]}.":".$_->[1] } @$res if $res;

    $xover->{'xref'} ||= $group . ":" . $xover->{'art'};

    ($xover->{'newsgroups'} = $xover->{'xref'}) =~ s/(:\d+)//g;
    $xover->{'newsgroups'} =~ s/\s+/,/g;

    push @xover, $xover;
  }
  return @xover;
}

sub get_article_message_id {
  my ($group,$artno) = @_;
  return unless exists $groups{$group};
  return get_article_message_id_from_xover($group,
                                           get_article_xover($group,$artno));
}

sub get_article_message_id_from_xover {
  my ($group,$xover) = @_;

  if ($xover->{'Message-ID'} && $xover->{'Message-ID'} =~ m/<(.+?)>/) {
    return $1;
  }
  return "$group-" . $xover->{'art'} . "\@$config{servername}";
}

sub get_group_and_article {
  my ($msgid) = @_;

  $msgid =~ s/\[at\]/@/;        # perl.org web obfuscation

  my $query = qq{SELECT grp,art FROM header WHERE msgid = ? LIMIT 1};

  my $res = $dbh->selectrow_hashref($query, undef, md5_hex($msgid));

  return $res ? ($groupsbyid{$res->{grp}}, $res->{art})
              : ($msgid =~ m/^(.+?)-(\d+)\@$config{servername}$/);

  return ($msgid =~ m/^(.+?)-(\d+)\@$config{servername}$/);
}

# this is a rather approximate conversion of 'wildmat' to perl.
sub wildmat ($$) {
  my ($expr, $string) = @_;
  $expr =~ s/(?<!\\)\./\\./g;
  $expr =~ s/(?<!\\)\$/\\\$/g;
  $expr =~ s/(?<!\\)\?/./g;
  $expr =~ s/(?<!\\)\*/.*/g;
  return $string =~ /^$expr$/;
}

sub incoming {
  my ($ihave) = shift;
  my ($newsgroups,$from,$to,$headers);
  $headers = '';
  my $bytes = 0;

  my $cancel_message = 0;

  # scoop up the headers
  alarm $config{timeout};
  while (<STDIN>) {
    last if /^(\.)?\r?\n$/s;
    s/\r\n$/\n/s;
    next if /^Path:/;
    ($newsgroups) = /^Newsgroups: (.+)/ unless $newsgroups;
    !$from 
      and /^From: (.*)/
      and ($from) = (Mail::Address->parse($1))[0]->address;
    next if (!$to && (($to) = /^To: (.+?)\s*$/s));
    next if /^Newsgroups:/;
    ++$cancel_message and next if /^Cancel:/;
    $bytes += length;
    $headers .= $_;
    alarm $config{timeout};
  }

  return respond($ihave ? 437 : 441,"posting failed - no newsgroups specified")
    unless $newsgroups;
  return respond($ihave ? 437 : 441,"posting failed - no from specified")
    unless $from;
  return respond($ihave ? 437 : 441,"posting failed - invalid from")
    unless $from =~ m/^.+?@[-.\w]+$/;

  my (@mailto);

  $newsgroups =~ s/\s//g;
  my (@newsgroups) = split /,/, lc $newsgroups;
  foreach (@newsgroups) {
    push @mailto, $groups{$_}->{mail};
  }
  return respond($ihave ? 437 : 441, "posting failed - no newsgroups known")
    unless @mailto;

  return respond($ihave ? 437 : 441, "cancels not accepted here")
    if $from =~ m/remove\.spam\@mindspring\.com/ or $cancel_message;

  open(FILE, "|-")
    || exec @mailinject, "-f$from", @mailto;
#Received: from unknown (HELO intranet.sbs.srv.br) (200.207.122.67)
#  by toye.p.sourceforge.net with SMTP; 8 Dec 2000 16:10:58 -0000
  # this header can be used to track evildoers
  print FILE "To: ", join(',', grep defined, (@mailto, $to)), "\n";
  print FILE $headers;
  print FILE "X-Posted-By: $ENV{TCPREMOTEIP}\n";
  print FILE "\n";

  alarm $config{timeout};
  while (<STDIN>) {
    s/\r\n$/\n/s;
    last if /^\.\n$/s;
    s/^\.//g;
    $bytes += length;
    print FILE;
    alarm $config{timeout};
  }
  alarm 0;

  close FILE
    or return respond($ihave ? 436 : 441, "posting failed -- qmail barfed!");

  foreach (@newsgroups) {
    note("/$_/new", "PUT", 200, $bytes);
  }

  respond($ihave ? 235 : 240,"article posted ok");
}

sub outgoing {
  my ($which,$restrict) = @_; # restrict: 0=stat, 1=header, 2=body, 3=all
  my ($id, $ggg); # message-id, group derived from message-id

  $which ||= $current; # use the current article if none was specified

  # handle specification by message-id
  if ($which && $which =~ m/^<(.+?)>$/) {
    $id = $1;
    ($ggg,$which) = get_group_and_article($id);
    unless ($ggg) {
      note("/msgid/$id", $restrict == 1 ? "HEAD" : "GET", 404);
      return respond(430, "no such article found");
    }
  }

  return respond(412,"no newsgroup has been selected")
    unless $ggg || $group;
  return respond(420,"no current article has been selected")
    unless $which;

  my $article = open_article($ggg || $group,$which);
  unless ($article) {
    note("/".($ggg||$group)."/$which", $restrict == 1 ? "HEAD" : "GET", 404);
    return respond($ggg ? 430 : 423, "no such article found");
  }

  my ($xover) = get_article_xover($ggg||$group, $which)
    or return fault("failed to get xover data for $which <$id>");

  # figure out the message-id and newsgroups
  $id ||= get_article_message_id_from_xover($group,$xover);

  if (!$restrict) {
    $current = $which unless $ggg;
    return respond(223, "$which <$id> article retrieved - request text separately");
  }
  elsif ($restrict == 1) {
    respond(221, "$which <$id> article retrieved - head follows");
  }
  elsif ($restrict == 2) {
    respond(222, "$which <$id> article retrieved - body follows");
  }
  elsif ($restrict == 3) {
    respond(220, "$which <$id> article retrieved - head and body follows");
  }

  # add some synthetic headers
  unless ($restrict == 2) {
    print "Newsgroups: ", $xover->{newsgroups} || $group, "\r\n";
    print "Path: $config{servername}\r\n";
    print "Xref: $config{servername} ", $xover->{xref}, "\r\n"
      if $xover->{xref};
    print "Followup-To: ", $groups{$ggg||$group}{followup}, "\r\n"
      if $groups{$ggg||$group}{followup};
  }

  my ($pasthead,$havedate,$havemsgid,$havesubj,$haveref);
  while (<$article>) {
    alarm $config{timeout};
    s/^\./../s; # fix lines with a leading dot
    s/(?<!\r)\n\z/\r\n/s; # fix lines to end with \r\n
    unless ($pasthead) {

      # regular nntp servers don't like this...
      next if m/^X-MS-(Has-Attach|TNEF-Correlator):\s*$/;
      next if m/^(Distribution|Followup-To|Keywords):\s*$/;

      next if /^From:/; # skip the from header, we add our own
      next if /^Path:/; # ""
      next if /^Newsgroups:/; # ""

      # add a space when it doesn't exist, should probably make that generic...
      s/^(Content-Type:)(\S)/$1 $2/;

      next if $havemsgid and m/^Message-ID:/i;  # skip duplicate msgids
      /^Message-ID/i && s/(?<=>) +//;  #trim trailing whitespace
      $havedate ||= m/^Date:/i;
      $havemsgid ||= s/^Message-ID:/Message-ID:/i;
      $havesubj ||= m/^Subject:/;
      $haveref ||= m/^References:/;
      if ($pasthead = m/^\r?\n$/s) {
        next if $restrict == 2;

	print "Approved: news\@$config{servername}\r\n" if $groups{$group||$ggg}->{moderated};
	print "From: ", $xover->{From}, "\r\n";
	print "Date: ",
	      POSIX::strftime("%a, %d %B %Y %H:%M:%S %z",
			      localtime((stat($article))[9])),
	      "\r\n"
	  unless $havedate;
	print "Message-ID: <$id>\r\n" unless $havemsgid;
	print "Subject: \r\n" unless $havesubj;
	print "References: ", $xover->{References}, "\r\n"
	  unless $haveref || !$xover->{References};

	if ($restrict == 1) {
	  note("/".($group||$ggg)."/$which", "HEAD");
	  return dot();
	}
      }
      next if $restrict == 2;
    }

    print;
  }
  if ($restrict == 3 and not $pasthead) {
    print "From: ", $xover->{From}, "\r\n";
    print "Date: ",
	  POSIX::strftime("%a, %d %B %Y %H:%M:%S %z",
			  localtime((stat($article))[9])),
	  "\r\n"
      unless $havedate;
    print "Message-ID: <$id>\r\n" unless $havemsgid;
    print "Subject: \r\n" unless $havesubj;
    print "References: ", $xover->{References}, "\r\n"
      unless $haveref || !$xover->{References};
    print "\r\n";
  }
  note("/$group/$which", "GET");
  dot();
}

=head1 COMMAND HANDLERS

=cut

sub article {
  outgoing(shift, 3);
}

sub body {
  outgoing(shift,2);
}

sub date {
  respond(111, POSIX::strftime("%Y%m%d%H%M%S", gmtime));
}

sub head {
  outgoing(shift,1);
}

sub help {
  respond(100, "help text follows");
  print "  ", join "\r\n  ", sort keys %commands;
  print "\r\n";
  note("/help");
  dot();
}

sub have_messageid {
  return scalar $dbh->selectrow_array("SELECT grp,art FROM header WHERE msgid = ?", undef, md5_hex(shift));
}

sub ihave {
  my ($messageid) = ($_[0] =~ m/^<(.+?)>$/);
  return respond(437, "article rejected - do not try again")
    if !$messageid || have_messageid($messageid);
  respond(335,"send article to be transferred. end with <CR-LF>.<CR-LF>");
  incoming(1);
}

sub group {
  my ($ggg) = lc shift
    or return respond(501, "no group specified");
  return respond(411, "no such news group")
    if !exists($groups{$ggg});

  my ($min, $max) = get_group_minmax($ggg)
    or return fault("unable to get max for $ggg");
    
  my $count = $max - $min + 1;

  # select the new group, reset the current article number
  $group = $ggg; $current = $min;

  respond(211, "$count $min $max $group");
}

sub last {
  return respond(412, "no newsgroup selected") unless $group;
  return respond(420, "no current article has been selected") unless $current;
  my ($min, $max) = get_group_minmax($group);
  return respond(422, "no previous article in this group")
    if $current == $min;

  # look up the message-id for the previous article
  my $id = get_article_message_id($group,--$current);

  respond(223, "$current <$id> article retrieved - request text separately");
}

sub list {
  my ($list) = lc shift || "active";
  my ($match) = shift;

  if ($list eq 'active') {
    respond(215, "list of newsgroups follows");
    for (sort keys %groups) {
      next if $match && !wildmat($match, $_);
      next if !$match && $groups{$_}->{'hidden'};
      my ($min,$max) = get_group_minmax($_);
      my $act = get_group_active($_);
      printf "%s %010d %010d %s\r\n", $_, $max, $min, $act if $max && $act;
    }
    note("/list/active");
    return dot();
  }
  elsif ($list eq 'active.times') {
    respond(215, 'group creations in form "name time who".');
    for (sort keys %groups) {
      next if $match && !wildmat($match, $_);
      next if $groups{$_}->{'hidden'};
      my $time = get_group_creation($_)
        or next;
      print "$_ $time news\r\n";
    }
    note("/active.times");
    return dot();
  }
  elsif ($list eq 'newsgroups') {
    respond(215, "list of newsgroups follows");
    for (sort keys %groups) {
      next if $match && !wildmat($match, $_);
      next if !$match && $groups{$_}->{'hidden'};
      my $desc = get_group_description($_)
        or next;
      print "$_ $desc\r\n";
    }
    note("/list/newsgroups");
    return dot();
  }

  elsif ($list eq 'overview.fmt') {
    respond(215, "order of fields in overview database");
    foreach (@overview) {
      print "$_:\r\n";
    }
    print "Xref:full\r\n";
    note("/list/overview.fmt");
    return dot();
  }

  elsif ($list eq 'distributions') {
    respond(215, "list of distributions follows");
    note("/list/distributions");
    return dot();
  }
  elsif ($list eq 'distrib.pats') {
    respond(215, "list of distrib.pats follows");
    note("/list/distrib.pats");
    return dot();
  }
  elsif ($list eq 'subscriptions') {
    respond(215, "list of suggested subscriptions follows");
    for (sort keys %groups) {
      print "$_\r\n" if get_group_recommend($_);
    }
    note("/list/subscriptions");
    return dot();
  }
  respond(501, "list type not understood");
  note("/list/$list","GET",404);
}

sub listgroup {
  my $ggg = shift || $group;
  respond(412, "not currently in newsgroup")
    if !$ggg;
  my ($min, $max) = get_group_minmax($ggg)
    or return fault("couldn't get information about group");
  $group = $ggg; $current = $min;
  respond(211, "list of article numbers to follow");
  alarm $config{timeout};
  for (my $i = $min; $i <= $max; $i++) {
    print "$i\r\n";
    alarm $config{timeout};
  }
  note("/$ggg");
  dot();
}

sub mode {
  my $mode = lc shift;
  if ($mode eq 'reader') {
    return respond(200,"hello, you can post");
  }
  if ($mode eq 'stream') { # shouldn't be necessary, so we don't support it
    return respond(200,"sure, why not?");
  }
  respond(501, "mode not understood");
}

sub next {
  return respond(412, "no newsgroup selected") unless $group;
  return respond(420, "no current article has been selected") unless $current;
  my ($min, $max) = get_group_minmax($group);
  return respond(421, "no next article in this group")
    if $current == $max;

  # look up the message-id for the next article
  my $id = get_article_message_id($group,++$current);

  respond(223, "$current <$id> article retrieved - request text separately");
}

sub newgroups {
  my ($date,$time,$gmt) = @_;
  return fault("invalid date or time format")
    unless $date && $time && (!$gmt || $gmt eq 'GMT' || $gmt eq 'UTC');

  my ($since);
  my ($y,$m,$d,$h,$i,$s) = (unpack("a2a2a2", $date), unpack("a2a2a2",$time));
  $y += 100 if $y < 70; # violation of US patent #5,806,063. bite me.
  if ($gmt) {
    $since = Time::Local::timegm($s,$i,$h,$d,$m-1,$y);
  }
  else {
    $since = Time::Local::timelocal($s,$i,$h,$d,$m-1,$y);
  }

  respond(231, "list of new newsgroups follows");
  for (sort keys %groups) {
    my $created = get_group_creation($_);
    next unless $created && $created > $since;
    my ($min, $max) = get_group_minmax($_);
    my $act = get_group_active($_);
    printf "%s %010d %010d %s\r\n", $_, $max, $min, $act if $max && $act;
  }
  note("/newgroups");
  dot();
}

sub newnews {
  respond(230, "list of new articles by message-id follows");
  note("/newnews");
  dot();
}

sub post {
  respond(340,"send article to be posted. end with <cr-lf>.<cr-lf>");
  incoming(0);
}

sub quit {
  respond(205, "closing connection - goodbye!");
  exit;
}

sub slave {
  respond(202, "slave status noted");
  # yeah, not really. nobody cares.
}

sub stat {
  outgoing(shift, 0);
}

sub xgtitle {
  my $match = shift;

  respond(282, "list of newsgroups follows");
  for (sort keys %groups) {
    next if $match && !wildmat($match, $_);
    my $desc = get_group_description($_)
      or next;
    print "$_ $desc\r\n";
  }
  note("/newsgroups".($match && "?q=$match"));
  dot();
}

# this serves triple-duty as xrover and xpat, too.
sub xhdr {
  my ($header,$range,$code,@pats) = @_;
  $code ||= 221;
  my ($ggg,$begin,$end);

  if ($range && $range =~ /^<(.+)>$/) {
    ($ggg,$begin) = get_group_and_article($1);
    if (!$ggg || !$begin) {
      note("/msgid/$1","HEAD",404);
      return respond(530,"no such article")
    }
  }
  else {
    return respond(412,"no news group currently selected")
      if !$group;
    return respond(520,"no current article selected")
      if !$range && !$current;
    $range && (($begin,$end) = split /-/, $range);
    $begin ||= $current;
  }

  # force the header into the case we use in the overview database
  foreach (keys %overview) {
    $header =~ s/^$_$/$_/i;
  }

  # short-circuit for headers not in overview database
  unless (exists $overview{$header}) {
    note("/$group?h=$header","GET",404);
    respond($code, "$header not in overview database, returning empty list");
    return dot();
  }

  my ($min,$max) = get_group_minmax($group);

  $begin = $min if ($begin && $begin < $min);
  $begin ||= $current;
  $end ||= defined $end ? $max : $begin;

  if (($begin > $max) || ($end && ($begin > $end)) || !($begin || $current)) {
    note("/$group?h=$header&b=$begin&e=$end","GET",204);
    return respond(420,"no article(s) selected");
  }

  respond($code, "$header follows for $begin to $end");
  alarm $config{timeout};
  my (@xover) = get_article_xover($group, $begin, $end);
  foreach my $xover (@xover) {
    foreach my $p (@pats) {
      next ARTICLE unless wildmat($p,$xover->{$header});
    }
    next unless $xover->{$header};
    print $xover->{'art'}," ",$xover->{$header}, "\r\n";
    #XXX fix support for multiple requested headers 
  }
  #XXX include @pats in log
  note("/$group?h=$header&b=$begin&e=$end");
  dot();
}

sub xover {
  my ($begin,$end) = split /-/, (shift||"");
  return respond(412,"no news group currently selected")
    if !$group;
  return respond(501,"most specify beginning of range")
    if defined $end && !$begin;
  my ($min,$max) = get_group_minmax($group);

  $begin = $min if ($begin && $begin < $min);
  $begin ||= $current;
  $end ||= defined $end ? $max : $begin;

  if (($begin > $max) || ($end && ($begin > $end)) || !($begin || $current)) {
    note("/$group?b=$begin&e=$end","GET",204);
    return respond(420,"no article(s) selected");
  }

  respond(224, "overview information follows for $begin to $end");
  alarm $config{timeout};
  my (@xover) = get_article_xover($group, $begin, $end);
  foreach my $xover (@xover) {
    print $xover->{'art'};
    foreach my $header (@overview) {
      print "\t", ($xover->{$header} || "");
    }
    print "\tXref: ", ($xover->{xref} || "");
    print "\r\n";
  }
  note("/$group?b=$begin&e=$end");
  dot();
}

sub xpat {
  xhdr(shift,shift,221,@_);
}

sub xrover {
  xhdr("References",shift,224,@_);
}

sub xpath {
  my $id = shift;
  if ($id && $id =~ m/^<(.+?)>$/) {
    my ($ggg,$begin) = get_group_and_article($1);
    respond(223, "$ggg/$begin");
  }
  else {
    respond(501, "invalid msgid");
  }

}

sub spool_feed {
  my $feed = shift or die "No feed specified...";

  $RESPOND = 0;

  open (STDIN, "<&6")  or die "could not reopen stdin: $!";
  open (STDOUT, ">&7") or die "could not reopen stdout: $!";


  $SIG{ALRM} = sub { respond(500, "Timeout!"),exit; };

  alarm 15;
  my $resp = <STDIN>;
  die "Did not get 200 from $feed [$resp]\n" unless $resp =~ m/^200/;

  warn "spooling $feed\n" if $TRACE;

  opendir DIR, "feeds/$feed" or die "could not opendir for $feed: $!";
  my @files = readdir DIR;
  closedir DIR;
  for my $file (sort @files) {
    next if $file =~ m/\.(tmp|spooling)$/;
    next if $file =~ m/^\./;
    next if $file =~ m/~$/;
    rename "feeds/$feed/$file", "feeds/$feed/$file.spooling" or next;
    -z "feeds/$feed/$file.spooling" and unlink "feeds/$feed/$file.spooling", next;

    open F, "feeds/$feed/$file.spooling" or die "could not open feeds/$feed/$file: $!";
    my $timestamp = time;
    open RESEND, ">feeds/$feed/$timestamp.$$.tmp"
      or die "could not open feeds/$feed/$timestamp.$$.tmp: $!";

    while (<F>) {
      chomp;
      my ($ggg, $artno) = split /\//;
      warn "sending $artno from $ggg\n" if $TRACE;

      my $xover = get_article_xover($ggg, $artno)
	or die fault("failed to get xover data for $artno");
      
      # figure out the message-id and newsgroups
      my $id = get_article_message_id_from_xover($group,$artno,$xover);

      alarm 10;
      
      print "IHAVE <$id>\r\n";

#          open ERR, ">>feeds/$feed.errors" or die "could not append to errors: $ !";
#          print ERR "$ggg/$artno offering <$id>\n";
#          close ERR or die "could not close error spool file";

      
  # 235 article transferred ok
  # 335 send article to be transferred.  End with <CR-LF>.<CR-LF>
  # 435 article not wanted - do not send it
  # 436 transfer failed - try again later
  # 437 article rejected - do not try again

      my $resp = <STDIN>;

      if ($resp =~ m/^335/) {
	warn "they want $artno, sending it!\n" if $TRACE;
	$group = $ggg;
	outgoing($artno, 3);
	my $resp = <STDIN>;
	$resp =~ s/\r\n//;
	#warn "$resp; next please!\n" and 
	next if ($resp =~ m/235/);
	if ($resp =~ m/^437/) {
	  open ERR, ">>feeds/$feed.errors" or die "could not append to errors: $!";
	  print ERR "$ggg/$artno $resp\n";
	  close ERR or die "could not close error spool file";
	  next;
	}
	warn "$resp; Didn't like it!\n" and next 
	print(RESEND "$ggg/$artno\n"), next if ($resp =~ m/^436/);
	die "Unknown response: [$resp]\n";
      }
      elsif ($resp =~ m/^435/) {
	warn "they don't want $artno ($resp), skipping it...\n" if $TRACE;
	next;
      }
      elsif ($resp =~ m/^436/) {
	print(RESEND "$ggg/$artno\n");
      }
      else {
	die "Unknown response: $resp";
      }
      
      alarm 0;
      
    }

    close RESEND;
    if (-z "feeds/$feed/$timestamp.$$.tmp") {
      unlink "feeds/$feed/$timestamp.$$.tmp";
    }
    else {
      rename "feeds/$feed/$timestamp.$$.tmp", "feeds/$feed/$timestamp.$$";
    }

    close F;

    unlink "feeds/$feed/$file.spooling" or die "could not unlink feeds/$feed/$file.spooling: $!";

  }

  return;
}


sub xversion {
  respond(200, "Colobus $Colobus::VERSION");
}


1;
