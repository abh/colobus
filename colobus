#!/pkg/bin/perl -w
# Copyright (c) 2001 Jim Winstead Jr. See the LICENSE file for details.
use strict;

$| = 1;

# this is designed to be run under tcpserver
# (http://cr.yp.to/ucspi-tcp.html)
# or inetd if you're into that sort of thing

package Colobus;
$Colobus::VERSION = "0.9/ask-0.6.3-dev";

my $TRACE = $ENV{TRACE};
$TRACE++ if -t STDIN and not defined $TRACE;

# spool_feeds will set this to 0 so it can send commands manually
my $RESPOND = 1;

use POSIX ();
use BerkeleyDB qw(DB_CREATE DB_LAST DB_RDONLY DB_INIT_CDB DB_INIT_MPOOL);
use Storable ();
use MLDBM qw(BerkeleyDB::Btree Storable);
use Mail::Address ();
use Time::Local ();
use Symbol ();
use File::Path qw(mkpath);
use Data::Dumper ();

use Colobus qw(%config %feeds %groups get_group_max open_article);

# setting this to qw(/usr/sbin/sendmail) should work, but DO NOT ADD -t!
my (@mailinject) = qw(/var/qmail/bin/qmail-inject -a);

my ($group,$current); # the current group and article number (state!)

Colobus::read_config("config");

#warn Data::Dumper->Dump([\%feeds, \%groups], [qw(feeds groups)]);

# these are the commands we understand, so far
my (@commands) = qw(article body date group head help ihave last list
                    listgroup mode next newgroups newnews post quit slave
                    stat xgtitle xhdr xover xpat xpath xrover xversion);
my (%commands); @commands{@commands} = ('') x @commands;
if ($config{disallow}) {
  foreach (map { s/^\s*(.+?)\s*$/$1/; $_; } split /,/, $config{disallow}) {
    delete $commands{$_};
  }
}

# these are the headers we track for the overview database
my (@overview) = qw(Subject From Date Message-ID References Bytes
                    Lines);
my (%overview); @overview{@overview} = ('') x @overview;

if (@ARGV && $ARGV[0] eq '-s') {
  spool_feed($ARGV[1]); exit;
}

if (@ARGV && $ARGV[0] eq '-u') {
  update_databases(); exit;
}

if (@ARGV && shift @ARGV eq '-r') {
  update_databases(1, @ARGV); exit;
}

if (@ARGV && $ARGV[0] eq '-d') {
  dump_databases(); exit;
}

$0 = "colobus - connection from $ENV{TCPREMOTEIP}, connected " . scalar localtime if $ENV{TCPREMOTEIP};

my $dbenv = new BerkeleyDB::Env
		-Flags => DB_INIT_CDB|DB_INIT_MPOOL|DB_CREATE,
  or die "failed to open bdb env: $! $BerkeleyDB::Error";

my $msgids = open_msgids($dbenv,1);

$SIG{ALRM} = sub { respond(500, "Timeout!"),exit; };

# output the greeting
# XXX provide an option so one could have tcpserver send the banner
respond(200, "$config{servername} - colobus $Colobus::VERSION ready - (posting ok).");

alarm $config{timeout};
while (<STDIN>) {
  $_ =~ s/\r?\n$//s; # advanced chomp
  warn "dispatching $_\n" if $TRACE;
  defined dispatch(split / +/, $_)
    or respond(500, "command '$_' not recognized");
  alarm $config{timeout};
}

sub dispatch {
  my ($cmd) = lc shift;

  if (exists $commands{$cmd}) {
    my ($result) = eval "&$cmd";
    warn $@ if $@;
    return $result if defined $result;
    return fault("command '$cmd' failed unexpectedly");
  }

  return;
}

sub respond {
  my ($code, $message) = @_;
  return "$code $message" unless $RESPOND;
  warn "$code $message\n" if $TRACE;
  return print "$code $message\r\n";
}

sub dot () {
  warn ".\n" if $TRACE;
  print ".\r\n";
}

sub fault {
  my ($msg) = shift || "program fault - command not performed";
  return respond(503, $msg);
}

sub get_group_active {
  my ($group) = lc shift
    or return;
  return 'n' unless exists $groups{$group} && $groups{$group}->{mail};
  return 'm' if exists $groups{$group}->{moderated};
  return 'y';
}

sub get_group_description {
  my ($group) = lc shift
    or return;
  return unless exists $groups{$group} && $groups{$group}->{desc};
  return $groups{$group}->{desc};
}

sub get_group_min {
  my ($group) = lc shift;
  return $groups{$group}->{first} || 1;
}

# we guess the group creation time from the modified time of the
# first message in the archive. should be close enough.
sub get_group_creation {
  my ($group) = lc shift
    or return;
  return unless exists $groups{$group};
  my $artno = get_group_min($group);
  my $file = sprintf("%s/archive/%d/%02d", $groups{$group}->{path},
                     int $artno / 100, $artno % 100);
  return (stat($file))[9];
}

sub get_group_recommend {
  my ($group) = lc shift
    or return;
  return exists $groups{$group} && exists $groups{$group}->{recommend};
}

use Symbol ();

sub get_article_xover {
  my ($group,$artno) = @_;
  return unless exists $groups{$group};
  unless (exists $groups{$group}->{db}) {
    my $db = open_group_xover($dbenv,$group,1);
    $groups{$group}->{db} = $db;
  }
  my $xover = $groups{$group}->{db}->{$artno};
  $xover ||= get_article_xover_from_file($group,$artno);
  if ($xover) {
    my $msgid = get_article_message_id_from_xover($group,$artno,$xover);
    if (exists $msgids->{$msgid}) {
      $xover->{xref} = join " ", keys %{$msgids->{$msgid}};
      ($xover->{newsgroups} = $xover->{xref}) =~ s/(:\d+)//g;
      $xover->{newsgroups} =~ s/\s+/,/g;
    }
  }
  return $xover;
}

sub get_article_xover_from_file {
  my ($group,$artno) = @_;
  return unless exists $groups{$group};
  my $article = open_article($group,$artno)
    or return;
  my (%xover);
  my $body  = 0;
  my $lines = 0;
  while (my $line = <$article>) {
    $body = 1 unless $body or $line =~ /\S/;
    last if $body and $xover{Lines};
    $lines++ if $body;
    unless ($body) {
      foreach my $header (keys %overview) {
	if ($line =~ m/^$header: (.+?)\r?\n/is) {
	  ($xover{$header} = $1) =~ s/\s/ /g;
	}
	if (!$xover{'References'} && $line =~ m/^In-Reply-To: (.+?)\r?\n/is) {
	  ($xover{'References'}) = ($1 =~ m/(<.+?>)/);
	}
      }
    }
  }

  $xover{'Lines'} ||= $lines;

  # make sure we have a message-id
  $xover{'Message-ID'} ||= "<$group-$artno\@$config{servername}>";

  # fix the From header
  my ($from) = Mail::Address->parse($xover{From});
  if ($from) {
    $xover{From} = eval { $from->address };
    $xover{From} .= " (".$from->name.")" if $from->name;
  }
  else {
    $xover{From} = "bogus\@$config{servername} (Unknown Sender)";
  }
  return \%xover;
}

sub get_article_message_id {
  my ($group,$artno) = @_;
  return unless exists $groups{$group};
  return get_article_message_id_from_xover($group,$artno,
                                           get_article_xover($group,$artno));
}

sub get_article_message_id_from_xover {
  my ($group,$artno,$xover) = @_;
  if ($xover->{'Message-ID'} && $xover->{'Message-ID'} =~ m/<(.+?)>/) {
    return $1;
  }
  return "$group-$artno\@$config{servername}";
}

sub get_group_and_article {
  my ($msgid) = @_;
  if (my $articles = $msgids->{$msgid}) {
    return split /:/, ((keys %$articles)[0]);
  }
  return ($msgid =~ m/^(.+?)-(\d+)\@$config{servername}$/);
}

# this is a rather approximate conversion of 'wildmat' to perl.
sub wildmat ($$) {
  my ($expr, $string) = @_;
  $expr =~ s/(?<!\\)\./\\./g;
  $expr =~ s/(?<!\\)\$/\\\$/g;
  $expr =~ s/(?<!\\)\?/./g;
  $expr =~ s/(?<!\\)\*/.*/g;
  return $string =~ /^$expr$/;
}

sub incoming {
  my ($ihave) = shift;
  my ($newsgroups,$from,$to,$headers);
  $headers = '';

  my $cancel_message = 0;

  # scoop up the headers
  alarm $config{timeout};
  while (<STDIN>) {
    last if /^(\.)?\r?\n$/s;
    s/\r\n$/\n/s;
    next if /^Path:/;
    ($newsgroups) = /^Newsgroups: (.+)/ unless $newsgroups;
    !$from 
      and /^From: (.*)/
      and ($from) = (Mail::Address->parse($1))[0]->address;
    next if (!$to && (($to) = /^To: (.+?)\s*$/s));
    next if /^Newsgroups:/;
    ++$cancel_message and next if /^Cancel:/; 
    $headers .= $_;
    alarm $config{timeout};
  }

  return respond($ihave ? 437 : 441,"posting failed - no newsgroups specified")
    unless $newsgroups;
  return respond($ihave ? 437 : 441,"posting failed - no from specified")
    unless $from;
  return respond($ihave ? 437 : 441,"posting failed - invalid from")
    unless $from =~ m/^.+?@[-.\w]+$/;

  my (@mailto);

  $newsgroups =~ s/\s//g;
  my (@newsgroups) = split /,/, lc $newsgroups;
  foreach (@newsgroups) {
    push @mailto, $groups{$_}->{mail};
  }
  return respond($ihave ? 437 : 441, "posting failed - no newsgroups known")
    unless @mailto;

  return respond($ihave ? 437 : 441, "cancels not accepted here")
    if $from =~ m/remove.spam\@mindspring.com/ or $cancel_message; 

  open(FILE, "|-")
    || exec @mailinject, "-f$from", @mailto;
#Received: from unknown (HELO intranet.sbs.srv.br) (200.207.122.67)
#  by toye.p.sourceforge.net with SMTP; 8 Dec 2000 16:10:58 -0000
  # this header can be used to track evildoers
  print FILE "To: ", join(',', grep defined, (@mailto, $to)), "\n";
  print FILE $headers;
  print FILE "X-Posted-By: $ENV{TCPREMOTEIP}\n";
  print FILE "\n";

  alarm $config{timeout};
  while (<STDIN>) {
    s/\r\n$/\n/s;
    last if /^\.\n$/s;
    s/^\.//g;
    print FILE;
    alarm $config{timeout};
  }
  alarm 0;

  close FILE
    or return respond($ihave ? 436 : 441, "posting failed -- qmail barfed!");

  respond($ihave ? 235 : 240,"article posted ok");
}

sub outgoing {
  my ($which,$restrict) = @_; # restrict: 0=stat, 1=header, 2=body, 3=all
  my ($id, $ggg); # message-id, group derived from message-id

  $which ||= $current; # use the current article if none was specified

  # handle specification by message-id
  if ($which && $which =~ m/^<(.+?)>$/) {
    $id = $1;
    ($ggg,$which) = get_group_and_article($id)
      or return respond(430, "no such article found");
  }

  return respond(412,"no newsgroup has been selected")
    unless $ggg || $group;
  return respond(420,"no current article has been selected")
    unless $which;

  my $article = open_article($ggg || $group,$which)
    or return respond($ggg ? 430 : 423, "no such article found");

  my $xover = get_article_xover($ggg||$group, $which)
    or return fault("failed to get xover data for $which <$id>");

  # figure out the message-id and newsgroups
  $id ||= get_article_message_id_from_xover($group,$which,$xover);

  if (!$restrict) {
    $current = $which unless $ggg;
    return respond(223, "$which <$id> article retrieved - request text separately");
  }
  elsif ($restrict == 1) {
    respond(221, "$which <$id> article retrieved - head follows");
  }
  elsif ($restrict == 2) {
    respond(222, "$which <$id> article retrieved - body follows");
  }
  elsif ($restrict == 3) {
    respond(220, "$which <$id> article retrieved - head and body follows");
  }

  # add some synthetic headers
  unless ($restrict == 2) {
    print "Newsgroups: ", $xover->{newsgroups} || $group, "\r\n";
    print "Path: $config{servername}\r\n";
    my $xref = $xover->{xref} 
      ? $xover->{xref}
      : "$group:$which";
    print "Xref: $config{servername} ", $xref, "\r\n"
  }

  my ($pasthead,$havedate,$havemsgid,$havesubj,$haveref);
  while (<$article>) {
    alarm $config{timeout};
    s/^\./../s; # fix lines with a leading dot
    s/(?<!\r)\n\z/\r\n/s; # fix lines to end with \r\n
    unless ($pasthead) {

      # regular nntp servers don't like this...
      next if m/^X-MS-(Has-Attach|TNEF-Correlator):\s*$/;
      next if m/^(Distribution|Followup-To|Keywords):\s*$/;

      next if /^From:/; # skip the from header, we add our own
      next if /^Path:/; # ""
      next if /^Newsgroups:/; # ""

      # add a space when it doesn't exist, should probably make that generic...
      s/^(Content-Type:)(\S)/$1 $2/;

      next if $havemsgid and m/^Message-ID:/i;  # skip duplicate msgids
      /^Message-ID/i && s/(?<=>) +//;  #trim trailing whitespace
      $havedate ||= m/^Date:/i;
      $havemsgid ||= s/^Message-ID:/Message-ID:/i;
      $havesubj ||= m/^Subject:/;
      $haveref ||= m/^References:/;
      if ($pasthead = m/^\r?\n$/s) {
        next if $restrict == 2;
	print "Approved: news\@$config{servername}\r\n" if $groups{$group||$ggg}->{moderated};
        print "From: ", $xover->{From}, "\r\n";
        print "Date: ",
              POSIX::strftime("%a, %d %B %Y %H:%M:%S %z",
                              localtime((stat($article))[9])),
              "\r\n"
          unless $havedate;
        print "Message-ID: <$id>\r\n" unless $havemsgid;
        print "Subject: \r\n" unless $havesubj;
        print "References: ", $xover->{References}, "\r\n"
	  unless $haveref || !$xover->{References};
        return dot() if $restrict == 1;
      }
      next if $restrict == 2;
    }

    print;
  }
  dot();
}

=head1 COMMAND HANDLERS

=cut

sub article {
  outgoing(shift, 3);
}

sub body {
  outgoing(shift,2);
}

sub date {
  respond(111, POSIX::strftime("%Y%m%d%H%M%S", gmtime));
}

sub head {
  outgoing(shift,1);
}

sub help {
  respond(100, "help text follows");
  print "  ", join "\r\n  ", sort keys %commands;
  print "\r\n";
  dot();
}

sub ihave {
  my ($messageid) = ($_[0] =~ m/^<(.+?)>$/);
  return respond(437, "article rejected - do not try again")
    if !$messageid || exists $msgids->{$messageid};
  respond(335,"send article to be transferred. end with <CR-LF>.<CR-LF>");
  incoming(1);
}

sub group {
  my ($ggg) = lc shift
    or return respond(501, "no group specified");
  return respond(411, "no such news group")
    if !exists($groups{$ggg});

  warn "$$ group $ggg\n" if $config{usagelog};

  my $max = get_group_max($ggg)
    or return fault("unable to get max for $ggg");
  my $min = get_group_min($ggg);
  my $count = $max - $min + 1;

  # select the new group, reset the current article number
  $group = $ggg; $current = get_group_min($group);

  respond(211, "$count $min $max $group");
}

sub last {
  return respond(412, "no newsgroup selected") unless $group;
  return respond(420, "no current article has been selected") unless $current;
  my $max = get_group_max($group);
  return respond(422, "no previous article in this group")
    if $current == get_group_min($group);

  # look up the message-id for the previous article
  my $id = get_article_message_id($group,--$current);

  respond(223, "$current <$id> article retrieved - request text separately");
}

sub list {
  my ($list) = lc shift || "active";
  my ($match) = shift;

  if ($list eq 'active') {
    respond(215, "list of newsgroups follows");
    for (sort keys %groups) {
      next if $match && !wildmat($match, $_);
      my $max = get_group_max($_);
      my $min = get_group_min($_);
      my $act = get_group_active($_);
      printf "%s %010d %010d %s\r\n", $_, $max, $min, $act if $max && $act;
    }
    return dot();
  }
  elsif ($list eq 'active.times') {
    respond(215, 'group creations in form "name time who".');
    for (sort keys %groups) {
      next if $match && !wildmat($match, $_);
      my $time = get_group_creation($_)
        or next;
      print "$_ $time news\r\n";
    }
    return dot();
  }
  elsif ($list eq 'newsgroups') {
    respond(215, "list of newsgroups follows");
    for (sort keys %groups) {
      next if $match && !wildmat($match, $_);
      my $desc = get_group_description($_)
        or next;
      print "$_ $desc\r\n";
    }
    return dot();
  }

  elsif ($list eq 'overview.fmt') {
    respond(215, "order of fields in overview database");
    foreach (@overview) {
      print "$_:\r\n";
    }
    print "Xref:full\r\n";
    return dot();
  }

  elsif ($list eq 'distributions') {
    respond(215, "list of distributions follows");
    return dot();
  }
  elsif ($list eq 'distrib.pats') {
    respond(215, "list of distrib.pats follows");
    return dot();
  }
  elsif ($list eq 'subscriptions') {
    respond(215, "list of suggested subscriptions follows");
    for (sort keys %groups) {
      print "$_\r\n" if get_group_recommend($_);
    }
    return dot();
  }
  respond(501, "list type not understood");
}

sub listgroup {
  my $ggg = shift || $group;
  respond(412, "not currently in newsgroup")
    if !$ggg;
  my $min = get_group_min($ggg);
  my $max = get_group_max($ggg)
    or return fault("couldn't get information about group");
  $group = $ggg; $current = $min;
  respond(211, "list of article numbers to follow");
  alarm $config{timeout};
  for (my $i = $min; $i <= $max; $i++) {
    print "$i\r\n";
    alarm $config{timeout};
  }
  dot();
}

sub mode {
  my $mode = lc shift;
  if ($mode eq 'reader') {
    return respond(200,"hello, you can post");
  }
  if ($mode eq 'stream') { # shouldn't be necessary, so we don't support it
    return respond(200,"sure, why not?");
  }
  respond(501, "mode not understood");
}

sub next {
  return respond(412, "no newsgroup selected") unless $group;
  return respond(420, "no current article has been selected") unless $current;
  my $max = get_group_max($group);
  return respond(421, "no next article in this group")
    if $current == $max;

  # look up the message-id for the next article
  my $id = get_article_message_id($group,++$current);

  respond(223, "$current <$id> article retrieved - request text separately");
}

sub newgroups {
  my ($date,$time,$gmt) = @_;
  return fault("invalid date or time format")
    unless $date && $time && (!$gmt || $gmt eq 'GMT' || $gmt eq 'UTC');

  my ($since);
  my ($y,$m,$d,$h,$i,$s) = (unpack("a2a2a2", $date), unpack("a2a2a2",$time));
  $y += 100 if $y < 70; # violation of US patent #5,806,063. bite me.
  if ($gmt) {
    $since = Time::Local::timegm($s,$i,$h,$d,$m-1,$y);
  }
  else {
    $since = Time::Local::timelocal($s,$i,$h,$d,$m-1,$y);
  }

  respond(231, "list of new newsgroups follows");
  for (sort keys %groups) {
    my $created = get_group_creation($_);
    next unless $created && $created > $since;
    my $max = get_group_max($_);
    my $min = get_group_min($_);
    my $act = get_group_active($_);
    printf "%s %010d %010d %s\r\n", $_, $max, $min, $act if $max && $act;
  }
  dot();
}

sub newnews {
  respond(230, "list of new articles by message-id follows");
  dot();
}

sub post {
  respond(340,"send article to be posted. end with <cr-lf>.<cr-lf>");
  incoming(0);
}

sub quit {
  respond(205, "closing connection - goodbye!");
  exit;
}

sub slave {
  respond(202, "slave status noted");
  # yeah, not really. nobody cares.
}

sub stat {
  outgoing(shift);
}

sub xgtitle {
  my $match = shift;

  respond(282, "list of newsgroups follows");
  for (sort keys %groups) {
    next if $match && !wildmat($match, $_);
    my $desc = get_group_description($_)
      or next;
    print "$_ $desc\r\n";
  }
  dot();
}

# this serves triple-duty as xrover and xpat, too.
sub xhdr {
  my ($header,$range,$code,@pats) = @_;
  $code ||= 221;
  my ($ggg,$begin,$end);

  if ($range && $range =~ /^<(.+)>$/) {
    ($ggg,$begin) = get_group_and_article($1);
    return respond(530,"no such article")
      if !$ggg || !$begin;
  }
  else {
    return respond(412,"no news group currently selected")
      if !$group;
    return respond(520,"no current article selected")
      if !$range && !$current;
    $range && (($begin,$end) = split /-/, $range);
    $begin ||= $current;
  }

  # force the header into the case we use in the overview database
  foreach (keys %overview) {
    $header =~ s/^$_$/$_/i;
  }

  # short-circuit for headers not in overview database
  unless (exists $overview{$header}) {
    respond($code, "$header not in overview database, returning empty list");
    return dot();
  }

  my $min = get_group_min($group);
  my $max = get_group_max($group);

  $begin = $min if ($begin && $begin < $min);
  $begin ||= $current;
  $end ||= defined $end ? $max : $begin;

  return respond(420,"no article(s) selected")
    if ($begin > $max) || ($end && ($begin > $end)) || !($begin || $current);

  respond($code, "$header follows for $begin to $end");
  ARTICLE: for ($begin..$end) {
    alarm $config{timeout};
    my $xover = get_article_xover($group,$_)
      or next;
    foreach (@pats) {
      next ARTICLE unless wildmat($_,$xover->{$header});
    }
    next unless $xover->{$header};
    print "$_ ", $xover->{$header}, "\r\n";
  }
  dot();
}

sub xover {
  my ($begin,$end) = split /-/, (shift||"");
  return respond(412,"no news group currently selected")
    if !$group;
  return respond(501,"most specify beginning of range")
    if defined $end && !$begin;
  my $min = get_group_min($group);
  my $max = get_group_max($group);

  $begin = $min if ($begin && $begin < $min);
  $begin ||= $current;
  $end ||= defined $end ? $max : $begin;

  return respond(420,"no article(s) selected")
    if ($begin > $max) || ($end && ($begin > $end)) || !($begin || $current);

  respond(224, "overview information follows for $begin to $end");
  for ($begin..$end) {
    alarm $config{timeout};
    my $xover = get_article_xover($group,$_)
      or next;
    print $_;
    foreach my $header (@overview) {
      print "\t", ($xover->{$header} || "");
    }
    print "\tXref: ", ($xover->{xref} || "");
    print "\r\n";
  }
  dot();
}

sub xpat {
  xhdr(shift,shift,221,@_);
}

sub xrover {
  xhdr("References",shift,224,@_);
}

sub xpath {
  my $id = shift;
  if ($id && $id =~ m/^<(.+?)>$/) {
    my ($ggg,$begin) = get_group_and_article($1);
    respond(223, "$ggg/$begin");
  } 
  else {
    respond(501, "invalid msgid");
  }

}

sub xversion {
  respond(200, "Colobus $Colobus::VERSION");
}

=head1 DATABASE HANDLING

=cut

sub open_group_xover {
  my ($env,$group,$ro) = @_;
  my (%xover);
  my $db = tie %xover, 'MLDBM', -Filename => "db/$group.db",
           -Env => $env,
           -Flags => $ro ? DB_RDONLY : DB_CREATE
    or die "unable to open database for $group: $! $BerkeleyDB::Error";

  $db->filter_fetch_key  ( sub { $_ = unpack("i", $_) } );
  $db->filter_store_key  ( sub { $_ = pack ("i", $_) } ) ;

  return \%xover;
}

sub open_msgids {
  my ($env,$ro) = @_;
  my (%msgid);
  my $db = tie %msgid, 'MLDBM', -Filename => "db/messageid.db",
           -Env => $env,
           -Flags => ($ro ? DB_RDONLY : DB_CREATE)
    or die "unable to open database: $! $BerkeleyDB::Error";

  return \%msgid;
}

sub update_databases {
  my ($env) = new BerkeleyDB::Env
                  -Flags => DB_INIT_CDB|DB_INIT_MPOOL|DB_CREATE
    or die "failed to create env: $! $BerkeleyDB::Error";

  my ($msgids) = open_msgids($env);

  my ($force, @groups) = @_;
  if (!@groups) {
    @groups = sort keys %groups;
  }

  my %feed_fh;
  my $timestamp = time;
  for my $feed (keys %feeds) {
    mkpath "feeds/$feed" unless -e "feeds/$feed";
    my $fh = Symbol::gensym();
    open ($fh, ">feeds/$feed/$timestamp.$$.tmp")
	or die "Could not open spool file for feed $feed: $!";
    $feed_fh{$feed} = $fh;
  }

  foreach (@groups) {
    print "updating $_ " if $TRACE;

    my $db = open_group_xover($env,$_);

    my $max = get_group_max($_);
    next unless $max;

    my ($last) = $max;
    1 until ($db->{$last} || !(--$last));

    $last = 0 if $force;

    my $first = ($last > 0) ? ($last+1) : get_group_min($_);
    if ($first > $max) {
      print "caught up.\n" if $TRACE;
      next;
    }
    print "$first-$max: " if $TRACE;

    my $last_num_length = 0;
    foreach my $artno ($first..$max) {
      if (($force and $TRACE) and !($artno % 25)) {
        print "\b" x $last_num_length, $artno;
	$last_num_length = length $artno;
      }
      for my $feed (keys %feeds) {
	my $fh = $feed_fh{$feed};
	local $| = 1;
	print $fh "$_/$artno\n" or die "could not print to fh for $feed: $!";
      }

      my $xover = get_article_xover_from_file($_,$artno)
        or next;
      $db->{$artno} = $xover;
      my $msgid = get_article_message_id_from_xover($_,$artno,$xover);
      my $articles = ($msgids->{$msgid} ||= {});
      $articles->{"$_:$artno"} = undef;
      $msgids->{$msgid} = $articles;
    }
    print "\b" x $last_num_length, $max if $TRACE;
    print " - done.\n" if $TRACE;
  }

  for my $feed (keys %feeds) {
    close $feed_fh{$feed} or die "could not close spool file for feed $feed: $!";
    rename "feeds/$feed/$timestamp.$$.tmp", "feeds/$feed/$timestamp.$$"
      or die "could not rename spool file for feed $feed: $!";
  }

}

sub spool_feed {
  my $feed = shift or die "No feed specified...";

  $RESPOND = 0;

  open (STDIN, "<&6")  or die "could not reopen stdin: $!";
  open (STDOUT, ">&7") or die "could not reopen stdout: $!";


  $SIG{ALRM} = sub { respond(500, "Timeout!"),exit; };

  alarm 15;
  my $resp = <STDIN>;
  die "Did not get 200 from $feed [$resp]\n" unless $resp =~ m/^200/;

  warn "spooling $feed\n" if $TRACE;

  opendir DIR, "feeds/$feed" or die "could not opendir for $feed: $!";
  my @files = readdir DIR;
  closedir DIR;
  for my $file (sort @files) {
    next if $file =~ m/\.(tmp|spooling)$/;
    next if $file =~ m/^\./;
    next if $file =~ m/~$/;
    rename "feeds/$feed/$file", "feeds/$feed/$file.spooling" or next;
    -z "feeds/$feed/$file.spooling" and unlink "feeds/$feed/$file.spooling", next;

    open F, "feeds/$feed/$file.spooling" or die "could not open feeds/$feed/$file: $!";
    my $timestamp = time;
    open RESEND, ">feeds/$feed/$timestamp.$$.tmp"
      or die "could not open feeds/$feed/$timestamp.$$.tmp: $!";

    while (<F>) {
      chomp;
      my ($ggg, $artno) = split /\//;
      warn "sending $artno from $ggg\n" if $TRACE;

      my $xover = get_article_xover($ggg, $artno)
	or die fault("failed to get xover data for $artno");
      
      # figure out the message-id and newsgroups
      my $id = get_article_message_id_from_xover($group,$artno,$xover);

      alarm 10;
      
      print "IHAVE <$id>\r\n";

#          open ERR, ">>feeds/$feed.errors" or die "could not append to errors: $ !";
#          print ERR "$ggg/$artno offering <$id>\n";
#          close ERR or die "could not close error spool file";

      
  # 235 article transferred ok
  # 335 send article to be transferred.  End with <CR-LF>.<CR-LF>
  # 435 article not wanted - do not send it
  # 436 transfer failed - try again later
  # 437 article rejected - do not try again

      my $resp = <STDIN>;

      if ($resp =~ m/^335/) {
	warn "they want $artno, sending it!\n" if $TRACE;
	$group = $ggg;
	outgoing($artno, 3);
	my $resp = <STDIN>;
	$resp =~ s/\r\n//;
	#warn "$resp; next please!\n" and 
	next if ($resp =~ m/235/);
	if ($resp =~ m/^437/) {
	  open ERR, ">>feeds/$feed.errors" or die "could not append to errors: $!";
	  print ERR "$ggg/$artno $resp\n";
	  close ERR or die "could not close error spool file";
	  next;
	}
	warn "$resp; Didn't like it!\n" and next 
	print(RESEND "$ggg/$artno\n"), next if ($resp =~ m/^436/);
	die "Unknown response: [$resp]\n";
      }
      elsif ($resp =~ m/^435/) {
	warn "they don't want $artno ($resp), skipping it...\n" if $TRACE;
	next;
      }
      elsif ($resp =~ m/^436/) {
	print(RESEND "$ggg/$artno\n");
      }
      else {
	die "Unknown response: $resp";
      }
      
      alarm 0;
      
    }

    close RESEND;
    if (-z "feeds/$feed/$timestamp.$$.tmp") {
      unlink "feeds/$feed/$timestamp.$$.tmp";
    }
    else {
      rename "feeds/$feed/$timestamp.$$.tmp", "feeds/$feed/$timestamp.$$";
    }

    close F;

    unlink "feeds/$feed/$file.spooling" or die "could not unlink feeds/$feed/$file.spooling: $!";

  }

  # Opens a directory named EXPR for processing by C<readdir()>,
  # C<telldir()>, C<seekdir()>, C<rewinddir()>, and C<closedir()>.
  # Returns TRUE if successful.  DIRHANDLEs have their own namespace
  # separate from FILEHANDLEs. 

  return;
}

sub dump_databases {
  my $env = new BerkeleyDB::Env(-Flags => DB_INIT_CDB|DB_INIT_MPOOL)
    or die "failed to create env: $! $BerkeleyDB::Error";
  my ($msgids) = open_msgids($env,1);
  print Data::Dumper::Dumper($msgids);

  foreach (sort keys %groups) {
    my $db = open_group_xover($env,$_,1);
    print Data::Dumper::Dumper($db);
  }
}

1;
